plugins {
    id 'org.springframework.boot' version '3.+'
    id 'io.spring.dependency-management' version '1.+'
    id 'war'
}

group 'com.escanan.ealden.race'
version '1.0.0-SNAPSHOT'

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.google.code.gson:gson:2.+'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.+'
    testImplementation 'io.cucumber:cucumber-java:7.+'
    testImplementation 'io.cucumber:cucumber-spring:7.+'
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.+'
    testImplementation 'org.hamcrest:hamcrest:2.+'
    testImplementation 'org.mockito:mockito-core:4.+'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.+'
    testImplementation 'io.github.bonigarcia:webdrivermanager:5.+'
}

java {
    sourceCompatibility = JavaLanguageVersion.of(21)
}

test {
    useJUnitPlatform()
}

tasks.register('cucumber', JavaExec) {
    dependsOn build

    def feature = 'src/test/resources'

    if (project.hasProperty('feature')) {
        feature = project.getProperty('feature')
    }

    mainClass = 'io.cucumber.core.cli.Main'
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
    args = ['--tags', 'not @wip', '--plugin', 'pretty', '--glue', project.group, feature]
}

tasks.register('run') {
    dependsOn bootRun
}
